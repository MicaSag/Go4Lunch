apply plugin: 'com.android.application'
apply plugin: 'io.fabric'                    // For Crashlytics installation
apply plugin: 'jacoco'                       //continuous integration with travis-ci and jacoco

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest']) {

    reports {
        xml.enabled = true
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }

    def fileFilter = ['**/R.class',
                      '**/R$*.class',
                      '**/BuildConfig.*',
                      '**/Manifest*.*',
                      '**/*Test*.*',
                      'android/**/*.*',
                      '**/data/models/*']

    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)

    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ])

    // Always regenerate report
    outputs.upToDateWhen { false }
}

jacoco {
    toolVersion = "0.8.1"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

// For Unit Test
tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

Properties properties = new Properties()
properties.load(project.rootProject.file("local.properties").newDataInputStream())
def GOOGLE_MAPS_API_KEY = properties.getProperty("GoogleMapsApiKey")


android {
    buildToolsVersion "28.0.3"
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.android.sagot.go4lunch"
        minSdkVersion 19
        targetSdkVersion 28
        multiDexEnabled true
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Google Maps Api Key
        // --> For source
        buildConfigField ( "String", "google_maps_api", '"'+GOOGLE_MAPS_API_KEY+'"' )
        // --> For Manifest
        manifestPlaceholders = [googleMapsApiKey: properties.getProperty("GoogleMapsApiKey")]
    }
    buildTypes {

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        // continuous integration with travis-ci and Jacoco
        debug {
            testCoverageEnabled true
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {

    // MultiDex
    implementation 'com.android.support:multidex:1.0.3'

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'  // Add appcompat for use ToolBar
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.android.support:recyclerview-v7:28.0.0'
    implementation 'com.android.support:customtabs:28.0.0'
    implementation 'com.android.support:support-media-compat:28.0.0'
    implementation 'com.android.support:support-v4:28.0.0'
    implementation 'com.android.support:design:28.0.0'
    //for use Navigation Drawer & Bottom Navigation View

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //Add easy permissions
    implementation 'pub.devrel:easypermissions:2.0.0'

    // Add ButterKnife library for more easily link views to activities/fragments
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Add Glide Library for show images
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.6.1'

    // FireBase Integration
    implementation 'com.google.firebase:firebase-core:16.0.6' // For FireBase Integration
    implementation 'com.firebaseui:firebase-ui-auth:4.2.1'    // For FireBase UI Authentication
    implementation 'com.firebaseui:firebase-ui-database:4.2.1' // For FireBase with UI Auth
    implementation 'com.firebaseui:firebase-ui-firestore:4.2.1' // For use FireStore
    implementation 'com.firebaseui:firebase-ui-storage:4.2.1' // For use
    implementation 'com.google.firebase:firebase-messaging:17.3.4' // For FireBase Messaging

    // FaceBook Authentication
    implementation 'com.facebook.android:facebook-android-sdk:4.38.1'

    //TWITTER LOGIN SUPPORT
    // Add the Twitter Core Kit//
    implementation 'com.facebook.android:facebook-android-sdk:4.x'

    // Required only if Twitter login support is required
    // Find the latest Twitter SDK releases here: https://goo.gl/E5wZvQ
    implementation 'com.twitter.sdk.android:twitter-core:3.3.0'

    // Add Api Google Play Service : Map
    implementation 'com.google.android.gms:play-services-maps:16.0.0'
    // Add Api Google Play Service : Places
    implementation 'com.google.android.gms:play-services-places:16.0.0'
    // Add Api Google Play Service : Location
    implementation 'com.google.android.gms:play-services-location:16.0.0'

    // Add Gson library for serialized and de-serialized object
    implementation 'com.google.code.gson:gson:2.8.2'

    // Add Rx java2 library for reactive development
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.7'

    // Add Retrofit Library ( REST client ) for used to make network requests
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.4.0'

    // Add Support Library for execute network requests with the Streams
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'

    // Crashlytic
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }

    // Google Maps Utils
    implementation 'com.google.maps.android:android-maps-utils:0.5+'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-contrib:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
    }
}

apply plugin: 'com.google.gms.google-services' // For FireBase Integration

